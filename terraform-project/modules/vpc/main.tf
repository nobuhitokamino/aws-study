####################
# VPC
####################

resource "aws_vpc" "main" {

  cidr_block = var.vpc_cidr

  enable_dns_support   = true
  enable_dns_hostnames = true

}
#####################
# public subnet
#####################
resource "aws_subnet" "public" {

  for_each = var.public_subnets

  vpc_id = aws_vpc.main.id

  cidr_block        = each.value.cidr
  availability_zone = each.value.az

  map_public_ip_on_launch = true

}

#######################
# private subnet
#######################
resource "aws_subnet" "private" {

  for_each = var.private_subnets

  vpc_id = aws_vpc.main.id

  cidr_block        = each.value.cidr
  availability_zone = each.value.az

}
#######################
# InternetGateway
#######################

resource "aws_internet_gateway" "igw" {

  vpc_id = aws_vpc.main.id

}
######################
# RouteTable
######################

resource "aws_route_table" "public" {

  vpc_id = aws_vpc.main.id

  route {

    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id

  }

}

resource "aws_route_table_association" "public" {

  for_each = aws_subnet.public

  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id

}